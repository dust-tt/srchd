# Erdős Problem 90

## Metadata

- **Status:** open
- **Prize:** $500
- **Formalized:** Yes ([Lean](https://github.com/google-deepmind/formal-conjectures/blob/main/FormalConjectures/ErdosProblems/90.lean))
- **Tags:** geometry, distances
- **OEIS:** [A186705](https://oeis.org/A186705)
- **Source:** [erdosproblems.com/90](https://www.erdosproblems.com/90)

## Proof Progress

- **Theorems/Lemmas:** 3
- **Definitions:** 3
- **Sorry statements:** 2
- **Completeness:** 66.7%
- **Has proofs:** Yes

## Problem Statement

Does every set of $n$ distinct points in $\mathbb{R}^2$ contain at most $n^{1+O(1/\log\log n)}$ many pairs which are distance 1 apart?

## Background

The unit distance problem. In [[Er94b]](javascript:void(0);) Erdős dates this conjecture to 1946. This would be the best possible, as is shown by a set of lattice points. It is easy to show that there are $O(n^{3/2})$ many such pairs. The best known upper bound is $O(n^{4/3})$, due to Spencer, Szemer&#233;di, and Trotter [[SST84]](javascript:void(0);). In [[Er83c]](javascript:void(0);) and [[Er85]](javascript:void(0);) Erdős offers \$250 for an upper bound of the form $n^{1+o(1)}$. Part of the difficulty of this problem is explained by a result of Valtr (see [[Sz16]](javascript:void(0);)), who constructed a metric on $\mathbb{R}^2$ and a set of $n$ points with $\gg n^{4/3}$ unit distance pairs (with respect to this metric). The methods of the upper bound proof of Spencer, Szemer&#233;di, and Trotter [[SST84]](javascript:void(0);) generalise to include this metric. Therefore to prove an upper bound better than $n^{4/3}$ some special feature of the Euclidean metric must be exploited. See a survey by Szemer&#233;di [[Sz16]](javascript:void(0);) for further background and related results. See also [[92]](https://www.erdosproblems.com/92), [[96]](https://www.erdosproblems.com/96), and [[605]](https://www.erdosproblems.com/605).

## Lean Formalization

```lean
/-
Copyright 2025 The Formal Conjectures Authors.

Licensed under the Apache License, Version 2.0 (the "License");
you may not use this file except in compliance with the License.
You may obtain a copy of the License at

    https://www.apache.org/licenses/LICENSE-2.0

Unless required by applicable law or agreed to in writing, software
distributed under the License is distributed on an "AS IS" BASIS,
WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
See the License for the specific language governing permissions and
limitations under the License.
-/

import FormalConjectures.Util.ProblemImports

/-!
# Erdős Problem 90: The unit distance problem

*Reference:* [erdosproblems.com/90](https://www.erdosproblems.com/90)
-/

open Filter
open scoped EuclideanGeometry

namespace Erdos90
open Finset

/--
Given a finite set of points, this function counts the number of **unordered pairs** of distinct
points that are at a distance of exactly 1 from each other.
-/
noncomputable def unitDistancePairsCount (points : Finset ℝ²) : ℕ :=
  (points.offDiag.filter (fun p => dist p.1 p.2 = 1)).card / 2


/--
The set of all possible numbers of unit distances for a configuration of $n$ points.
-/
noncomputable def unitDistanceCounts (n : ℕ) : Set ℕ :=
  {unitDistancePairsCount points | (points : Finset ℝ²) (_ : points.card = n)}

/--
This lemma confirms that the set of possible unit distance counts is bounded above, which
ensures that taking the supremum (`sSup`) is a well-defined operation. The trivial upper bound is
the total number of pairs of points, $\binom{n}{2}$.
-/
@[category test, AMS 52]
theorem unitDistanceCounts_BddAbove (n : ℕ) : BddAbove <| unitDistanceCounts n := by
  unfold Erdos90.unitDistanceCounts
  unfold Erdos90.unitDistancePairsCount
  use n.choose 2
  rintro _ ⟨points, rfl, rfl⟩
  rw [points.card.choose_two_right]
  gcongr
  refine (card_filter_le _ _).trans_eq ?_
  rw [offDiag_card, Nat.mul_sub_left_distrib, mul_one]


/--
The **maximum number of unit distances** determined by any set of $n$ points in the plane.
This function is often denoted as $u(n)$ in combinatorics.
-/
noncomputable def maxUnitDistances (n : ℕ) : ℕ :=
  sSup (unitDistanceCounts n)


/--
Does every set of $n$ distinct points in $\mathbb{R}^2$ contain at most
$n^{1+O(\frac{1}{\log\log n})}$ many pairs which are distance $1$ apart?
-/
@[category research open, AMS 52]
theorem erdos_90 : answer(sorry) ↔ ∃ (O : ℕ → ℝ) (hO : O =O[atTop] (fun n => 1 / (n : ℝ).log.log)),
    (fun n => (maxUnitDistances n : ℝ)) =ᶠ[atTop] fun (n : ℕ) => (n : ℝ) ^ (1 + O n) := by
  sorry

-- TODO(firsching): add the statements from the rest of the page.

end Erdos90

```

## Notes

unit distance problem
